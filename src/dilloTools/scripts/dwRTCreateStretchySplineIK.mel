//Title:	dwRTCreateStretchySplineIK.mel
//Author: 	David Walden
//		www.davidwalden.com
//		dwalden74@hotmail.com
//
//Date:		March 31, 2004
//
//Description: Creates stretchy spline IK setup. User can choose between stretching by scaling or translation.
//		Currently only supports stretching on the X-axis.
global proc dwRTCreateStretchySplineIK (string $startJoint, 
					string $endJoint, 
					string $prefix, 
					int $clusterCheck, 
					int $stretchType,
					int $rootOnCurve,
					int $autoCreateRootAxis,
					int $autoParentCurve,
					int $snapCurveToRoot,
					int $autoCreateCurve,
					int $autoSimplifyCurve,
					int $noSpans,
					string $twistType,
					string $curve)
{
//Get initial selection.
string $selection[] = `ls -sl`;

//Report errors for bad input.
if ((! `objExists $startJoint`) || (`nodeType $startJoint` != "joint"))
	error "Start joint specified incorrectly. Try again.";
if ((! `objExists $endJoint`) || (`nodeType $endJoint` != "joint"))
	error "End joint specified incorrectly. Try again.";
if ($startJoint == $endJoint)
	error "Start and end joints are the same. Try again.";
//Report error if naming prefix is invalid.
if (! `dwRTValidateName $prefix`)
	error "Naming prefix invalid. Remove special characters and try again.";
if (! $stretchType)
	error "Specify a \"Stretch Type\" in the UI.";
//Check if user has deselected "Auto Create Curve" but has incorrectly specified the curve to use.
if ((! $autoCreateCurve) && (!`objExists $curve`))	
	error "Curve specified incorrectly. Try again.";


//If there압 no curve, then create the ikHandle with autoCreateCurve....
string $ikSplineIKHandle[];
if (! `objExists $curve`) 
	{
	$ikSplineIKHandle = `ikHandle 
				-solver ikSplineSolver 
				-n ($prefix + "SplineIK_Handle") 
				-sj $startJoint
				-ee $endJoint
				-rootOnCurve $rootOnCurve
				-createRootAxis $autoCreateRootAxis
				-parentCurve $autoParentCurve
				-snapCurve $snapCurveToRoot
				-createCurve $autoCreateCurve
				-simplifyCurve $autoSimplifyCurve
				-numSpans $noSpans
				-twistType $twistType`;
	}
//...otherwise use the curve specified.
else
	{
	$ikSplineIKHandle = `ikHandle 
				-solver ikSplineSolver 
				-n ($prefix + "SplineIK_Handle") 
				-sj $startJoint
				-ee $endJoint
				-rootOnCurve $rootOnCurve
				-createRootAxis $autoCreateRootAxis
				-parentCurve $autoParentCurve
				-snapCurve $snapCurveToRoot
				-createCurve $autoCreateCurve
				-simplifyCurve $autoSimplifyCurve
				-numSpans $noSpans
				-twistType $twistType
				-curve $curve`;
	}

	
string $joints[] = `ikHandle -q -jl $ikSplineIKHandle[0]`;
int $sizeJoints = `size($joints)`;

//Add spine end joint as the last index in the joint list....
$joints[$sizeJoints] = $endJoint;

//...and refresh size variable.
$sizeJoints = `size($joints)`;

rename $ikSplineIKHandle[1] ($prefix + "SplineIK_Effector");

//We need to assign $splineIKCurve variable. This will differ depending on whether we았e specified a curve to use for
//the splineIK, or whether we았e used $autoCreateCurve to create it automatically.
string $splineIKCurve;
if (! `objExists $curve`) 
	$splineIKCurve = `rename $ikSplineIKHandle[2] ($prefix + "SplineIK_Curve")`;
else 
	$splineIKCurve = `ikHandle -q -curve ($prefix + "SplineIK_Handle")`;
	
	
//Create spline clusters if desired.
if ($clusterCheck)
	{
	//GET LIST OF CVS IN CURVE.
	select -r ($splineIKCurve + ".cv[*]");
	string $splineIKCurveCVs[] = `filterExpand -ex true -sm 28`;
	int $sizeClusters = `size($splineIKCurveCVs)`;
	
	string $createSplineIKCluster[];   //String array which creates clusters.
	string $splineIKClusters[];	   //String array which will hold Cluster names- $createSplineIKCluster[0]
	string $splineIKClusterHandles[]; //String array which will hold Cluster Handle names- $createSplineIKCluster[1]
	int $i = 0;
	for ($cv in $splineIKCurveCVs)
		{
		select -r $cv;
		$createSplineIKCluster = `cluster -n ($prefix + "splineIKCluster" + $i + "_")`;
		$splineIKClusters[$i] = $createSplineIKCluster[0];
		$splineIKClusterHandles[$i] = $createSplineIKCluster[1];
		//setAttr ($splineIKClusterHandles[$i] + ".v") 0;
		$i++;
		}
	}



//Do stretchy setup.
//Create arclength to get length of curve.
string $curveInfo = `arclen -ch 1 $splineIKCurve`;
$curveInfo = `rename $curveInfo ($prefix + $curveInfo)`;
float $curveLength = `getAttr ($curveInfo + ".arcLength")`;

//Create divide node to calculate percentage difference in original curve length and actual curve length.
string $stretchDivideNode = `createNode -n ($prefix + "Back_Stretch_Divide") multiplyDivide`;
setAttr ($stretchDivideNode + ".operation") 2;
//Divide actual length by original length. (Outputs a value <1 if shorter, >1 if longer than original lenght.)
connectAttr ($curveInfo + ".arcLength") ($stretchDivideNode + ".input1X");
setAttr ($stretchDivideNode + ".input2X") $curveLength;
	

//Do stretch setup for each joint...	
string $backStretchMultiplyNode;
//If stretch by scaling...
if ($stretchType == 1)
	{
	//Start index on first joint, and continue until 2nd-to-last joint.
	for ($i = 0; $i < ($sizeJoints - 1); $i++)
		{
		//Multiply the $stretchDivideNode압 output by the original length of the joint. This output will drive the joint압 translation.
		$stretchMultiplyNode = `createNode -n ($prefix + $joints[$i] + "_Stretch_Multiply") multiplyDivide`;
		setAttr ($stretchMultiplyNode + ".operation") 1;
		//Connect $stretchDivideNode....
		connectAttr ($stretchDivideNode + ".outputX") ($stretchMultiplyNode + ".input1X");
		
		//Set $stretchMultiplyNode압 2nd input to joint압 original .sx value (1)...
		setAttr ($stretchMultiplyNode + ".input2X") 1;
		//Currently stretching is available only along the X-axis.
		connectAttr -f ($stretchMultiplyNode + ".outputX") ($joints[$i] + ".sx");
		}
	}

//If stretch by translation...
if ($stretchType == 2)
	{
	//Start index on second joint, and continue until the end.
	for ($i = 1; $i < $sizeJoints; $i++)
		{
		//Multiply the $stretchDivideNode압 output by the original length of the joint. This output will drive the joint압 translation.
		$stretchMultiplyNode = `createNode -n ($prefix + $joints[$i] + "_Stretch_Multiply") multiplyDivide`;
		setAttr ($stretchMultiplyNode + ".operation") 1;
		//Connect $stretchDivideNode....
		connectAttr ($stretchDivideNode + ".outputX") ($stretchMultiplyNode + ".input1X");
		
		//Set $stretchMultiplyNode압 2nd input to joint압 original .tx value...
		setAttr ($stretchMultiplyNode + ".input2X") `getAttr ($joints[$i] + ".tx")`;
		//Currently stretching is available only along the X-axis.
		connectAttr -f ($stretchMultiplyNode + ".outputX") ($joints[$i] + ".tx");
		}
	}	

//Reselect initial selection.
select -r $selection;

}//End of proc.